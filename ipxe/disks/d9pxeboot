#!ipxe
set esc:hex 1b
set bold ${esc:string}[1m
set boldoff ${esc:string}[22m
set fg_gre ${esc:string}[32m
set fg_cya ${esc:string}[36m
set fg_whi ${esc:string}[37m
set version 0.01

:start
echo ${bold}${fg_cya}DEGREE9 PXE BOOTLOADER ${fg_whi}v${version}${boldoff}
prompt --key 0x1b --timeout 5000 Launching D9 AUTOBOOT, press ${bold}esc${boldoff} to abort... && goto main || goto autoboot

:autoboot
prompt --key f --timeout 5000 Press ${bold}f${boldoff} for failsafe... && goto failsafe || console --x 1024 --y 768 --picture https://cdn-images-1.medium.com/max/2000/1*lDe58GEhrklob0TXmQ3KkQ.png
dhcp || goto main

:main
menu Degree9 Boot Menu
item local Load local config (USB)
item remote Load remote config (HTTP/HTTPS)
item dhcp Configure network automatically (DHCP)
item netconfig Configure network manually (Static)
item localboot Hardware boot sequence (BIOS/EFI)
choose --default local select-menu && goto ${select-menu} || goto error

:failsafe
menu Degree9 Failsafe Menu
item localboot Continue hardware boot sequence
item netconfig Manual network configuration
item retry Reload D9 Bootloader
item debug D9 Boot Shell (debug)
item reboot Reboot System
choose --default retry failsafe_choice || goto error
goto ${failsafe_choice}

:dhcp
echo Configuring network using DHCP...
dhcp || goto netconfig
goto main

:netconfig
echo Network Configuration:
echo Available interfaces...
ifstat
imgfree
echo -n Set network interface [net0]: ${} && read net
isset ${net} || set net net0
echo -n IP: && read ${net}/ip
echo -n Subnet mask: && read ${net}/netmask
echo -n Gateway: && read ${net}/gateway
echo -n DNS: && read dns
ifopen ${net}
echo Attempting to load menu...
goto menu || goto failsafe

:menu
echo Booting from HTTPS...
set conn_type https
set ERR_HTTPS Booting via HTTPS has failed...
chain --autofree https://boot.degree9.io/ || echo ${ERR_HTTPS}
sleep 2
echo Booting from HTTP...
set conn_type http
set ERR_HTTP Booting via HTTP has failed...
chain --autofree http://boot.degree9.io/ || echo ${ERR_HTTP}
goto retry

:localboot
echo Continuing hardware boot sequence...
exit

:retry
console
goto start

:reboot
echo Rebooting...
reboot

:debug
console
echo ${bold}${fg_cya}DEGREE9 BOOT SHELL ${fg_whi}v${version}${boldoff}
echo Type "exit" to return to menu...
shell
goto failsafe

:error
echo An unexpected error occurred...
goto retry
