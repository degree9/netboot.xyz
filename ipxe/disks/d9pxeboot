#!ipxe
set esc:hex 1b
set bold ${esc:string}[1m
set boldoff ${esc:string}[22m
set fg_gre ${esc:string}[32m
set fg_cya ${esc:string}[36m
set fg_whi ${esc:string}[37m
set version 0.01

:start
echo ${bold}${fg_cya}DEGREE9 PXE BOOTLOADER ${fg_whi}v${version}${boldoff}
prompt --key f --timeout 4000 Hit the ${bold}f${boldoff} key to open failsafe menu... && goto failsafe || goto dhcp

:dhcp
echo
dhcp || goto netconfig
goto menu

:failsafe
menu Degree9 Failsafe Menu
item localboot Boot to local drive
item netconfig Manual network configuration
item retry Retry boot
item debug iPXE Debug Shell
item reboot Reboot System
choose failsafe_choice || exit
goto ${failsafe_choice}

:netconfig
echo Network Configuration:
echo Available interfaces...
ifstat
imgfree
echo -n Set network interface [net0]: ${} && read net
isset ${net} || set net net0
echo -n IP: && read ${net}/ip
echo -n Subnet mask: && read ${net}/netmask
echo -n Gateway: && read ${net}/gateway
echo -n DNS: && read dns
ifopen ${net}
echo Attempting to load menu...
goto menu || goto failsafe

:menu
echo Booting from HTTPS...
set conn_type https
set ERR_HTTPS Booting via HTTPS has failed...
chain --autofree https://boot.degree9.io/menu.ipxe || echo ${ERR_HTTPS}
sleep 2
echo Booting from HTTP...
set conn_type http
set ERR_HTTP Booting via HTTP has failed...
chain --autofree http://boot.degree9.io/menu.ipxe || echo ${ERR_HTTP}
goto localboot

:localboot
echo Continuing hardware boot sequence...
exit

:retry
goto start

:reboot
echo Rebooting...
reboot
goto start

:debug
echo DEGREE9 PXE SHELL
echo Type "exit" to return to menu
shell
goto failsafe
